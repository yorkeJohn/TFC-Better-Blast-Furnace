plugins {
    id "java"
    id "idea"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "net.minecraftforge.gradle" version "[6.0,6.2)"
    id "org.parchmentmc.librarian.forgegradle" version "1.+"
    id "org.spongepowered.mixin" version "0.7.+"
}

// mod info
def modId = "tfcbetterbf"
def modVersion = "1.0.0"

// dependency versions
def minecraftVersion = "1.20.1"
def forgeVersion = "47.1.3"
def tfcVersion = "5356073"
def patchouliVersion = "1.20.1-81-FORGE"

base {
    group = "com.tadashi"
    version = modVersion
    archivesName = "TFCBetterBlastFurnace-${minecraftVersion}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://maven.blamejared.com"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}"
    implementation fg.deobf("curse.maven:tfc-302973:${tfcVersion}")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouliVersion}")
}

minecraft {
    mappings channel: "parchment", version: "2023.09.03-1.20.1"
    copyIdeResources = true

    runs {
        configureEach {
            args "-mixin.config=${modId}.mixins.json"
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }

        client {
        }

        server {
            args "--nogui"
        }

        gameTestServer {
        }

        data {
            workingDirectory project.file("run-data")
            args "--mod", modId, "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
        }
    }
}

mixin {
    add sourceSets.main, "${modId}.refmap.json"
}

tasks.named("jar", Jar).configure {
    manifest {
        attributes([
                "Implementation-Version": project.version,
                "MixinConfigs"          : "${modId}.mixins.json"
        ])
    }

    finalizedBy "reobfJar"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}
